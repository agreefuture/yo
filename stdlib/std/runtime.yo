// runtime.yo

impl runtime {

    // Note: type ids are not guaranteed to be the same across different compilations of the same program
    static fn getTypeId(obj: any): int {
        ret obj[0] & 0xfffff;
    }



    //
    // Reference Counting
    //

    // Increment an object's retain count by 1
    static fn retain(obj: any): any {
        ret runtime::_setRetainCount(obj, runtime::getRetainCount(obj) + 1);
    }


    // Decrement an object's retain count by 1
    // If the object's new retain count is 0,
    // we call the object's dealloc method and free it from the heap
    static fn release(obj: any): any {
        if runtime::getRetainCount(obj) == 1 {
            runtime::dealloc(obj);
            runtime::free(obj);
        } else {
            runtime::_setRetainCount(obj, runtime::getRetainCount(obj) - 1);
        }
        ret obj;
    }


    static fn _setRetainCount(obj: any, newValue: int): any {
        val typeId: int = runtime::getTypeId(obj);
        val deallocAddress: int = obj[0] >> 40;

        obj[0] = (deallocAddress << 40);
        obj[0] = obj[0] | (newValue << 20);
        obj[0] = obj[0] | typeId;

        //obj[0] = (deallocAddress << 40) | (newValue << 20) | typeId;

        ret obj;
    }


    static fn getRetainCount(obj: any): int {
        val retainCount: int = obj[0] & 0xfffff00000;
        ret retainCount >> 20;
    }


    // default `dealloc` function called for types who don't specify their own
    static fn _dealloc(obj: any): void {}
}
