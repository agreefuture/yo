use ":C/string";
use ":std/memory";

struct String {
    ptr: *i8,
    len: size_t
}

impl String {
    /// Initialize a new String from a char pointer
    /// The length of the String will be determined by the first null byte found
    fn init(self: &String, ptr: *i8) {
        self.ptr = strdup(ptr);
        self.len = strlen(ptr);
    }

    /// Initialize a new String with the first `len` characters pointed to by `ptr`
    fn init(self: &String, ptr: *i8, len: size_t) {
        self.ptr = alloc<i8>(len);
        memcpy(self.ptr, ptr, len);
        self.len = len;
    }

    /// Initialize a String by copying another String's contents
    fn init(self: &String, other: &String) {
        self.ptr = strdup(other.ptr);
        self.len = other.len;
    }

    fn c_str(self: &String) -> *i8 {
        return self.ptr;
    }

    fn length(self: &String) -> size_t {
        return self.len;
    }

    fn append(self: &String, other: String) {
        let newLength = self.len + other.len;
        self.ptr = realloc(self.ptr, newLength + 1);
        memcpy(self.ptr + self.len, other.ptr, other.len);
        self.ptr[newLength] = '\0';
        self.len = newLength;
    }

    fn print_desc(self: &String) {
        printf(b"[String ptr: %p len: %lli text: %s]\n", self.ptr, self.len, self.ptr);
    }

    fn dealloc(self: &String) {
        free(self.ptr);
    }
}
